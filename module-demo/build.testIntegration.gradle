project(':module-demo') {
    description = 'Defines behavior for integration test'

    configurations {
        testIntegrationCompile.extendsFrom testCompile
        testIntegrationRuntime.extendsFrom testRuntime
    }

    sourceSets {
        testIntegration {
            groovy {
                srcDir file("src/testIntegration/groovy")
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
            }
            java {
                srcDir file("src/testIntegration/java")
                compileClasspath += main.output + test.output
                runtimeClasspath += main.output + test.output
            }
            resources.srcDir file('src/testIntegration/resources')
        }
    }

    task testIntegration(type: Test) {
        classpath = sourceSets.testIntegration.runtimeClasspath
        testClassesDir = sourceSets.testIntegration.output.classesDir

        // never consider integration tests run up to date
        outputs.upToDateWhen { false }

        // By default the report would overwrite unit test report
        reports.junitXml.destination = "$buildDir/reports/testIntegration"
        reports.html.destination = "$buildDir/reports/testIntegration"
    }
}